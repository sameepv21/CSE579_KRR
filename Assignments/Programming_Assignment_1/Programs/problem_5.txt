% Define the Sudoku board.
1 {cell(R, C, V): V = 1..9} 1 :- R = 1..9, C = 1..9.

% Given initial values on the Sudoku board.
cell(1,3,6). cell(1, 7, 9).
cell(2,2,7). cell(2, 8, 8).
cell(3,1,3). cell(3, 3, 2). cell(3, 5, 8). cell(3, 7, 7). cell(3, 9, 4).
cell(4, 5, 9).
cell(5, 3, 8). cell(5, 4, 3). cell(5, 5, 6). cell(5, 6, 4). cell(5, 7, 2).
cell(6, 5, 7).
cell(7, 1, 8). cell(7, 3, 4). cell(7, 5, 2). cell(7, 7, 5). cell(7, 9, 1).
cell(8, 2, 2). cell(8, 8, 4).
cell(9, 3, 7). cell(9, 7, 8).

% No two cells on the same column can have the same value.
:- cell(R1, C, V), cell(R2, C, V), R1 != R2.

% No two cells on the same row can have the same value.
:- cell(R, C1, V), cell(R, C2, V), C1 != C2.

% No two cell in the same subgrid can have the same value.
:- cell(R1, C1, V), cell(R2, C2, V), (R1-1)/3 == (R2-1)/3, (C1-1)/3 == (C2-1)/3, R1 != R2, C1 != C2.

% No two cells in same offset locations can have the same value.
:- cell(R1, C1, V), cell(R2, C2, V), (R1-1)\3 == (R2-1)\3, (C1-1)\3 == (C2-1)\3, R1 != R2, C1 != C2.

#show cell/3.